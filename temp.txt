use Text::Markov;

my ( $nick, $channel ) = 'P6NotABot', '#perl6-recon';

my $mc = Text::Markov.new;
/\S/ and $mc.feed($_) for 'story.txt'.IO.lines;

await IO::Socket::Async.connect('localhost', 6667).then({
    my $sock = .result;
    $sock.print(qq:to/END/
        NICK $nick
        USER $nick $nick irc.freenode.net :Really not a bot
        JOIN $channel
        END
    );

    Supply.interval( 5 ).tap({
        spurt 'report.txt', "[{DateTime.now}] Still alive!\n", :append;
    });

    react {
        whenever $sock.Supply {
            .say;
            if /^':' <-[:]>+ 'PRIVMSG ' $channel ' :' $<said>=(.+)/ {
                $mc.feed( ~$<said> );
                ~$<said> ~~ /$nick/ and $sock.print(
                    ":$nick!~$nick@localhost PRIVMSG $channel "
                    ~ ":{$mc.read.substr(0, 200)}\n"
                );
            }
        }
    }
});